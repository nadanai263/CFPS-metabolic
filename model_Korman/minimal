// libAntimony v2.9.4
// Compartments and Species:
species  atp, adp, nad, nadh, ppi, pppi, pep, pyr, m, P, Pmat

// Reactions:
 
_J11: 3 pep + 3 adp -> 3 pyr + 3 atp; VmaxPyk * (pep*adp - pyr*atp/Keq11)/(Kadp11*Kpep11) * 1/((1+adp/Kadp11)*(1+pep/Kpep11)+(1+atp/Katp11)*(1+pyr/Kpyr11)-1);
_J22: 2 pppi -> 4 ppi; VmaxPpase * (pppi - ppi*ppi/Keq22)/Kpppi22 * 1/((1+pppi/Kpppi22)+(1+ppi/Kppi22)*(1+ppi/Kppi22)-1); 
_J23: 4 nadh -> 4 nad; VmaxNoxe * nadh/(Knadh23+nadh);
_Jdegpep: pep -> ppi; 0.001*pep;

_JTX: 170 atp -> 170 adp + 170 ppi + m; Vmaxtx*d/(d+KTX)*atp/(atp+Katp);
_Jdeg: m -> ; kdeg*m;
_JTL: 904 atp -> 904 adp + 904 ppi + P; Vmaxtl*m/(m+KTL)*atp/(atp+Katp);
_JMat: P -> Pmat; kmat*P;

// Species initializations
atp = 2;
adp = 0.0;
nad = 0.25;
nadh = 0.0;
ppi = 25.0;
pppi = 0.0;
pep = 33.0;
pyr = 0.0;
m = 0.0; // uM
P = 0.0; // uM
Pmat = 0.0; // uM

// Parameter initializations:
// 226 residues for deGFP
// 678 bp

d = 0.000001; // mM template DNA
Vmaxtx = 0.00002; // mM GFP mRNA/min, maximal transcription rate
Vmaxtl = 0.000002 // mM GFP/min, maximal translation rate
KTL = 0.0001; // mM, Michaelis-Menten constant for translation
KTX = 0.00001; // mM, Michaelis-Menten constant for transcription
kmat = 0.0005; // s^-1, deGFP maturation rate
kdeg = 0.0001; // s^-1, mRNA degradation rate
Katp = 1; // ATP transition Km


// R11
VmaxPyk = 1.0;
Kadp11 = 0.3; 
Kpep11 = 0.3;
Katp11 = 1.0;
Kpyr11 = 1.0;
Keq11 = 5000.0;

// R22
VmaxPpase = 20.0;
Kpppi22 = 0.1;
Kppi22 = 0.1;
Keq22 = 10.0;

// R23
VmaxNoxe = 1.0;
Knadh23 = 0.1;

Vmax= 1.0;