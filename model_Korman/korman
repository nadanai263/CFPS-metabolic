// libAntimony v2.9.4
// Compartments and Species:
species  atp, adp, nad, nadh, nadp, nadph, co2, ppi, pppi, coa, glc, g6p, f6p, fbp, gap, dhap, bpg, p3g, p2g, pep, pyr, accoa, acaccoa, hmgcoa, mev, mvp, mpp, ipp, dmapp, gpp, lim

// Reactions:
_J1: 1.5 atp + 1.5 glc -> 1.5 g6p + 1.5 adp; VmaxHex * (atp*glc - g6p*adp/Keq1)/(Katp1*Kglc1) * 1/((1+atp/Katp1)*(1+glc/Kglc1)+(1+adp/Kadp1)*(1+g6p/Kg6p1)-1);
_J2: 1.5 g6p -> 1.5 f6p; VmaxPgi * (g6p - f6p/Keq2)/(Kg6p2) * 1/((1+g6p/Kg6p2)+(1+f6p/Kf6p2)-1);
_J3: 1.5 f6p + 1.5 atp -> 1.5 fbp + 1.5 adp; VmaxPfk * (f6p*atp - fbp*adp/Keq3)/(Katp3*Kf6p3) * 1/((1+atp/Katp3)*(1+f6p/Kf6p3)+(1+adp/Kadp3)*(1+fbp/Kfbp3)-1);
_J4: 1.5 fbp -> 1.5 gap + 1.5 dhap; VmaxFba * (fbp - gap*dhap/Keq4)/(Kfbp4) * 1/((1+fbp/Kfbp4)+(1+dhap/Kdhap4)*(1+gap/Kgap4)-1);
_J5: 1.5 dhap -> 1.5 gap; VmaxTpi * (dhap - gap/Keq5)/Kdhap5 * 1/((1+dhap/Kdhap5)+(1+gap/Kgap5)-1); 
_J6: 3 gap + 3 ppi + nad -> 3 bpg + nadh; VmaxGap * (gap*nad*ppi - bpg*nadh/Keq6)/(Kgap6*Knad6*Kppi6) * 1/((1+gap/Kgap6)*(1+nad/Knad6)*(1+ppi/Kppi6)+(bpg/Kbpg6)*(1+nadh/Knadh6)-1);
_J7: 3 gap + 3 ppi + 2 nadp -> 3 bpg + 2 nadph; VmaxmGap * (gap*nadp*ppi - bpg*nadph/Keq7)/(Kgap7*Knadp7*Kppi7) * 1/((1+gap/Kgap7)*(1+nadp/Knadp7)*(1+ppi/Kppi7)+(bpg/Kbpg7)*(1+nadph/Knadph7)-1); 
_J8: 3 bpg + 3 adp -> 3 p3g + 3 atp; VmaxPgk * (bpg*adp - p3g*atp/Keq8)/(Kbpg8*Kadp8) * 1/((1+bpg/Kbpg8)*(1+adp/Kadp8)+(1+p3g/Kp3g8)*(1+atp/Katp8)-1); 
_J9: 3 p3g -> 3 p2g; VmaxPgm * (p3g - p2g/Keq9)/Kp3g9 * 1/((1+p3g/Kp3g9)+(1+p2g/Kp2g9)-1); 
_J10: 3 p2g -> 3 pep; VmaxEno * (p2g - pep/Keq10)/Kp2g10 * 1/((1+p2g/Kp2g10)+(1+pep/Kpep10)-1);
_J11: 3 pep + 3 adp -> 3 pyr + 3 atp; VmaxPyk * (pep*adp - pyr*atp/Keq11)/(Kadp11*Kpep11) * 1/((1+adp/Kadp11)*(1+pep/Kpep11)+(1+atp/Katp11)*(1+pyr/Kpyr11)-1);
_J12: 3 pyr + 3 coa + 3 nad -> 3 accoa + 3 nadh + 3 co2; VmaxPdh * (coa*nad*pyr - accoa*co2*nadh/Keq12)/(Kcoa12*Knad12*Kpyr12) * 1/((1+coa/Kcoa12)*(1+nad/Knad12)*(1+pyr/Kpyr12)+(1+accoa/Kaccoa12)*(1+co2/Kco212)*(1+nadh/Knadh12)-1);
_J13: 2 accoa -> coa + acaccoa; VmaxThl * (accoa*accoa - coa*acaccoa/Keq13)/(Kaccoa13*Kaccoa13) * 1/((1+accoa/Kaccoa13)*(1+accoa/Kaccoa13)+(1+acaccoa/Kacaccoa13)*(1+coa/Kcoa13)-1);
_J14: acaccoa + accoa -> coa + hmgcoa; VmaxHmgs * (acaccoa*accoa - coa*hmgcoa/Keq14)/(Kacaccoa14*Kaccoa14) * 1/((1+acaccoa/Kacaccoa14)*(1+accoa/Kaccoa14)+(1+hmgcoa/Khmgcoa14)*(1+coa/Kcoa14)-1);
_J15: hmgcoa + 2 nadph -> mev + coa + 2 nadp; VmaxHmgr * (hmgcoa*nadph*nadph - mev*coa*nadp*nadp/Keq15)/(Khmgcoa15*Knadph15*Knadph15) * 1/((1+hmgcoa/Khmgcoa15)*(1+nadph/Knadph15)*(1+nadph/Knadph15)+(1+mev/Kmev15)*(1+coa/Kcoa15)*(1+nadp/Knadp15)*(1+nadp/Knadp15)-1);
_J16: mev + atp -> mvp + adp; VmaxMvk * (mev*atp - mvp*adp/Keq16)/(Kmev16*Katp16) * 1/((1+mev/Kmev16+mvp/Kmvp16)*(1+atp/Katp16+adp/Kadp16));
_J17: mvp + atp -> mpp + adp; VmaxPmvk * (mvp*atp - mpp*adp/Keq17)/(Kmvp17*Katp17) * 1/((1+mvp/Kmvp17+mpp/Kmpp17)*(1+atp/Katp17+adp/Kadp17)); 
_J18: mpp + atp -> ipp + co2 + adp + ppi; VmaxMdc * (mpp*atp - adp*co2*ipp*ppi/Keq18)/(Kmpp18*Katp18) * 1/((1+mpp/Kmpp18)*(1+atp/Katp18)+(1+adp/Kadp18)*(1+co2/Kco218)*(1+ipp/Kipp18)*(1+ppi/Kppi18)-1);
_J19: ipp -> dmapp; VmaxIdi * (ipp - dmapp/Keq19)/Kipp19 * 1/((1+ipp/Kipp19)+(1+dmapp/Kdmapp19)-1);
_J20: dmapp + ipp -> gpp + pppi; VmaxFS82F * (dmapp*ipp - gpp*pppi/Keq20)/(Kdmapp20*Kipp20) * 1/((1+dmapp/Kdmapp20)*(1+ipp/Kipp20)+(1+gpp/Kgpp20)*(1+pppi/Kpppi20)-1);
_J21: gpp -> lim + pppi; VmaxLim * (gpp - lim*pppi/Keq21)/Kgpp21 * 1/((1+gpp/Kgpp21)+(1+lim/Klim21)*(1+pppi/Kpppi21)-1); 
_J22: 2 pppi -> 4 ppi; VmaxPpase * (pppi - ppi*ppi/Keq22)/Kpppi22 * 1/((1+pppi/Kpppi22)+(1+ppi/Kppi22)*(1+ppi/Kppi22)-1); 
_J23: 4 nadh -> 4 nad; VmaxNoxe * nadh/(Knadh23+nadh);

// Species initializations
atp = 4.0;
adp = 0.0;
nad = 0.25;
nadh = 0.0;
nadp = 1.5;
nadph = 0.0; 
co2 = 0.0; 
ppi = 25.0;
pppi = 0.0;
coa = 1.5;
glc = 500.0; 
g6p = 0.0;
f6p = 0.0;
fbp = 1.0;
gap = 0.0;
dhap = 0.0;
bpg = 0.0;
p3g = 0.0;
p2g = 0.0;
pep = 0.0;
pyr = 0.0;
accoa = 0.0;
acaccoa = 0.0;
hmgcoa = 0.0;
mev = 0.0;
mvp = 0.0;
mpp = 0.0;
ipp = 0.0;
dmapp = 0.0;
gpp = 0.0;
lim = 0.0;

// Parameter initializations:

// R1
VmaxHex = 0.05;
Katp1 = 0.1; 
Kglc1 = 0.15;
Kadp1 = 0.1;
Kg6p1 = 0.1;
Keq1 = 1000;

// R2
VmaxPgi = 1.0;
Kg6p2 = 0.3;
Kf6p2 = 0.15;
Keq2 = 0.5;

//R3
VmaxPfk = 0.5;
Katp3 = 0.1;
Kf6p3 = 0.03;
Kadp3 = 0.1;
Kfbp3 = 0.1;
Keq3 = 300;

// R4
VmaxFba = 1.0;
Kfbp4 = 0.015;
Kdhap4 = 0.1;
Kgap4 = 0.1;
Keq4 = 0.2;

// R5
VmaxTpi = 0.25;
Kdhap5 = 1.0;
Kgap5 = 1.0;
Keq5 = 0.04;

// R6
VmaxGap = 0.5;
Kgap6 = 1.0;
Knad6 = 0.05;
Kppi6 = 0.5;
Kbpg6 = 2.0;
Knadh6 = 0.1;
Keq6 = 0.07;

// R7
VmaxmGap = 10.0;
Kgap7 = 1.0;
Knadp7 = 0.05;
Kppi7 = 0.5;
Kbpg7 = 2.0;
Knadph7 = 0.1;
Keq7 = 0.07;

// R8
VmaxPgk = 2.0;
Kbpg8 = 0.1;
Kadp8 = 0.1;
Kp3g8 = 2.2;
Katp8 = 3.0; 
Keq8 = 3200.0;

// R9
VmaxPgm = 1.0;
Kp3g9 = 0.5;
Kp2g9 = 0.1;
Keq9 = 0.19;

// R10;
VmaxEno = 1.0;
Kp2g10 = 0.1;
Kpep10 = 1.0;
Keq10 = 6.7;

// R11
VmaxPyk = 1.0;
Kadp11 = 0.3; 
Kpep11 = 0.3;
Katp11 = 1.0;
Kpyr11 = 1.0;
Keq11 = 5000.0;

// R12
VmaxPdh = 10.0;
Kcoa12 = 0.1;
Knad12 = 0.1;
Kpyr12 = 0.5;
Kaccoa12 = 0.1;
Kco212 = 0.1;
Knadh12 = 0.1;
Keq12 = 5000.0;

// R13
VmaxThl = 1.0;
Kaccoa13 = 0.4;
Kacaccoa13 = 0.1;
Kcoa13 = 0.1;
Keq13 = 0.05;

// R14
VmaxHmgs = 1.0;
Kacaccoa14 = 0.01;
Kaccoa14 = 0.4;
Khmgcoa14 = 0.5;
Kcoa14 = 0.5;
Keq14 = 50.0;

// R15
VmaxHmgr = 1.0;
Khmgcoa15 = 0.02;
Knadph15 = 0.1;
Kmev15 = 1.0;
Kcoa15 = 0.3;
Knadp15 = 0.3;
Keq15 = 50.0;

// R16
VmaxMvk = 5.0;
Kmev16 = 0.07;
Katp16 = 0.5;
Kmvp16 = 1.5;
Kadp16 = 0.1;
Keq16 = 10.0;

// R17
VmaxPmvk = 5.0;
Kmvp17 = 0.008;
Katp17 = 0.14;
Kmpp17 = 0.01;
Kadp17 = 0.4;
Keq17 = 2.0;

// R18
VmaxMdc = 5.0;
Kmpp18 = 0.1;
Katp18 = 0.06;
Kadp18 = 0.3;
Kco218 = 5.0;
Kipp18 = 0.5;
Kppi18 = 0.5;
Keq18 = 10;

// R19
VmaxIdi = 1.0;
Kipp19 = 0.004;
Kdmapp19 = 0.05;
Keq19 = 0.77

// R20
VmaxFS82F = 1.0;
Kdmapp20 = 0.005;
Kipp20 = 0.005;
Kgpp20 = 4.0;
Kpppi20 = 5.0;
Keq20 = 200.0;

// R21
VmaxLim = 5.0;
Kgpp21 = 0.002;
Klim21 = 2.0;
Kpppi21 = 0.5;
Keq21 = 5000.0;

// R22
VmaxPpase = 20.0;
Kpppi22 = 0.1;
Kppi22 = 0.1;
Keq22 = 10.0;

// R23
VmaxNoxe = 1.0;
Knadh23 = 0.1;