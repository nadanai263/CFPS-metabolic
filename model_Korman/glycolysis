// libAntimony v2.9.4
// Compartments and Species:
species  atp, adp, nad, nadh, ppi, pppi, glc, g6p, f6p, fbp, gap, dhap, bpg, p3g, p2g, pep, pyr

// Reactions:
_J1: 1.5 atp + 1.5 glc -> 1.5 g6p + 1.5 adp; VmaxHex * (atp*glc - g6p*adp/Keq1)/(Katp1*Kglc1) * 1/((1+atp/Katp1)*(1+glc/Kglc1)+(1+adp/Kadp1)*(1+g6p/Kg6p1)-1);
_J2: 1.5 g6p -> 1.5 f6p; VmaxPgi * (g6p - f6p/Keq2)/(Kg6p2) * 1/((1+g6p/Kg6p2)+(1+f6p/Kf6p2)-1);
_J3: 1.5 f6p + 1.5 atp -> 1.5 fbp + 1.5 adp; VmaxPfk * (f6p*atp - fbp*adp/Keq3)/(Katp3*Kf6p3) * 1/((1+atp/Katp3)*(1+f6p/Kf6p3)+(1+adp/Kadp3)*(1+fbp/Kfbp3)-1);
_J4: 1.5 fbp -> 1.5 gap + 1.5 dhap; VmaxFba * (fbp - gap*dhap/Keq4)/(Kfbp4) * 1/((1+fbp/Kfbp4)+(1+dhap/Kdhap4)*(1+gap/Kgap4)-1);
_J5: 1.5 dhap -> 1.5 gap; VmaxTpi * (dhap - gap/Keq5)/Kdhap5 * 1/((1+dhap/Kdhap5)+(1+gap/Kgap5)-1); 
_J6: gap + ppi + nad -> bpg + nadh; VmaxGap * (gap*nad*ppi - bpg*nadh/Keq6)/(Kgap6*Knad6*Kppi6) * 1/((1+gap/Kgap6)*(1+nad/Knad6)*(1+ppi/Kppi6)+(bpg/Kbpg6)*(1+nadh/Knadh6)-1);
_J8: 3 bpg + 3 adp -> 3 p3g + 3 atp; VmaxPgk * (bpg*adp - p3g*atp/Keq8)/(Kbpg8*Kadp8) * 1/((1+bpg/Kbpg8)*(1+adp/Kadp8)+(1+p3g/Kp3g8)*(1+atp/Katp8)-1); 
_J9: 3 p3g -> 3 p2g; VmaxPgm * (p3g - p2g/Keq9)/Kp3g9 * 1/((1+p3g/Kp3g9)+(1+p2g/Kp2g9)-1); 
_J10: 3 p2g -> 3 pep; VmaxEno * (p2g - pep/Keq10)/Kp2g10 * 1/((1+p2g/Kp2g10)+(1+pep/Kpep10)-1);
_J11: 3 pep + 3 adp -> 3 pyr + 3 atp; VmaxPyk * (pep*adp - pyr*atp/Keq11)/(Kadp11*Kpep11) * 1/((1+adp/Kadp11)*(1+pep/Kpep11)+(1+atp/Katp11)*(1+pyr/Kpyr11)-1);
_J22: 2 pppi -> 4 ppi; VmaxPpase * (pppi - ppi*ppi/Keq22)/Kpppi22 * 1/((1+pppi/Kpppi22)+(1+ppi/Kppi22)*(1+ppi/Kppi22)-1); 
_J23: 4 nadh -> 4 nad; VmaxNoxe * nadh/(Knadh23+nadh);

// Species initializations
atp = 4.0;
adp = 0.0;
nad = 0.25;
nadh = 0.0;
ppi = 25.0;
pppi = 0.0;
glc = 500.0; 
g6p = 0.0;
f6p = 0.0;
fbp = 1.0;
gap = 0.0;
dhap = 0.0;
bpg = 0.0;
p3g = 0.0;
p2g = 0.0;
pep = 0.0;
pyr = 0.0;

// Parameter initializations:

// R1
VmaxHex = 0.05;
Katp1 = 0.1; 
Kglc1 = 0.15;
Kadp1 = 0.1;
Kg6p1 = 0.1;
Keq1 = 1000;

// R2
VmaxPgi = 1.0;
Kg6p2 = 0.3;
Kf6p2 = 0.15;
Keq2 = 0.5;

//R3
VmaxPfk = 0.5;
Katp3 = 0.1;
Kf6p3 = 0.03;
Kadp3 = 0.1;
Kfbp3 = 0.1;
Keq3 = 300;

// R4
VmaxFba = 1.0;
Kfbp4 = 0.015;
Kdhap4 = 0.1;
Kgap4 = 0.1;
Keq4 = 0.2;

// R5
VmaxTpi = 0.25;
Kdhap5 = 1.0;
Kgap5 = 1.0;
Keq5 = 0.04;

// R6
VmaxGap = 0.5;
Kgap6 = 1.0;
Knad6 = 0.05;
Kppi6 = 0.5;
Kbpg6 = 2.0;
Knadh6 = 0.1;
Keq6 = 0.07;

// R8
VmaxPgk = 2.0;
Kbpg8 = 0.1;
Kadp8 = 0.1;
Kp3g8 = 2.2;
Katp8 = 3.0; 
Keq8 = 3200.0;

// R9
VmaxPgm = 1.0;
Kp3g9 = 0.5;
Kp2g9 = 0.1;
Keq9 = 0.19;

// R10;
VmaxEno = 1.0;
Kp2g10 = 0.1;
Kpep10 = 1.0;
Keq10 = 6.7;

// R11
VmaxPyk = 1.0;
Kadp11 = 0.3; 
Kpep11 = 0.3;
Katp11 = 1.0;
Kpyr11 = 1.0;
Keq11 = 5000.0;

// R22
VmaxPpase = 20.0;
Kpppi22 = 0.1;
Kppi22 = 0.1;
Keq22 = 10.0;

// R23
VmaxNoxe = 1.0;
Knadh23 = 0.1;